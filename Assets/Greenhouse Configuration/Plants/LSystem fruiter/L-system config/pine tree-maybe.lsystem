#axiom S
#iterations 50

#symbols /\&^F$`@

## flags set based on the pollination state
#runtime hasAnther 1
#runtime isPollinated 0

## composition definitions. define directives evaluate in-order, these directives rely on subsequent definitions to complete
#define flowerMeshIndex (flowerColorOuterIndex + flowerColorInnerIndex * 3)

## variables controlled by genes
#define primaryBranchInternodes 5
#define flowerFailureChance 0.25
#define stamenSize 0.6
## ranges from 0 to 2. 0 is least ripe, 2 is most ripe.
#define fruitColorIndex 1
#define flowerColorInnerIndex 1
#define flowerColorOuterIndex 1


## variables defined for utility
#define shootHeight 5
#define internodeHeight 7
#define leafAge 3
#define leavesPerPetiole 5
#define petioleLeafAngularDist 70

#define flowerAge 7
#define timeToFruit 10


## S is the Shoot symbol
#symbols S
#matches S
S -> T(primaryBranchInternodes, 3)


## T is a terminal bud
#symbols T
#matches T
T(x, y) : x > 0 && y <= 0-> I(internodeHeight)[&(30)P(x - 1, 0)][\(180)&(30)P(x - 1, 0)]\(137)T(x - 1, 3)
T(x, y) : y > 0 -> T(x, y - 1)
T(x, y) : x <= 0 -> J(internodeHeight)

## P is a branch
#symbols P
#matches P
P(x, y) : x > 0 && y <= 0 -> I(internodeHeight)[+(40)X(x)][-(40)X(x)]P(x - 1, 3)
P(x, y) : y > 0 -> P(x, y - 1)
P(x, y) : x <= 0 -> 

#define sideBranchFactor 0.1
## X is a bud which can turn into a branch
#symbols X
#matches X
P(sideBranchFactor)     | X(x) -> P(x - 1, 3)
P(1 - sideBranchFactor) | X(x) -> 

## I is an internode
#symbols I
#matches I
I(x) : x > 0 -> F@(0.98)$(0.01)I(x - 1)
I(x) : x <= 0 ->

$(x) -> $(x * .9)

## organs

#define leafExpression [&&L][/(180)&&L]
#symbols N
#matches N
N -> leafExpression

## l is a leaf
#symbols Ll
#matches Ll
L -> [l(1, leafAge)]
L(x) -> [l(1, x)]
l(x, y) : x < y -> l(x + 1, y)

## K is a flower
#symbols K
#matches K
         K(y, x) : x < flowerAge -> K(y, x + 1)
C(age) < K(y, x) : age >= timeToFruit ->

## A is an anther
#symbols A
#matches A
         A(x) : (x < flowerAge * stamenSize) && (hasAnther > 0) -> A(x + stamenSize)
         A(x) : hasAnther < 1 ->
C(age) < A(y) : age >= timeToFruit ->













